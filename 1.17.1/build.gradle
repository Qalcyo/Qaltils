/*
 * Wyvtils, a utility mod for 1.8.9.
 * Copyright (C) 2021 Wyvtils
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id "fabric-loom" version "0.10-SNAPSHOT"
    id "io.github.juuxel.loom-quiltflower-mini" version "1.1.0"
}
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.github.gmazzo.buildconfig"
apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "maven-publish"
apply plugin: "java"

compileKotlin {
    kotlinOptions {
        jvmTarget = "16"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16
archivesBaseName = project_name + " [1.17.1]"
version = project_ver

loom {
    accessWidenerPath = file("src/main/resources/wyvtils.accesswidener")
    mixin {
        defaultRefmapName = "mixins.wyvtils.refmap.json"
    }
}

dependencies {
    shade include(project(path: ":core")) {
        exclude(module: "essential-1.8.9-forge")
        exclude(module: "vigilance-1.8.9-forge")
        exclude(module: "elementa-1.8.9-forge")
        exclude(module: "universalcraft-1.8.9-forge")
        exclude(module: "kotlin-reflect")
        exclude(module: "kotlin-stdlib-jdk8")
        exclude(module: "kotlin-stdlib-jdk7")
        exclude(module: "kotlin-stdlib")
        exclude(module: "kotlinx-coroutines-core")
        exclude(module: "keventbus")
    }
    implementation("com.github.Wyvest:keventbus:e8e05ea")
    minecraft "com.mojang:minecraft:1.17.1"
    mappings "net.fabricmc:yarn:1.17.1+build.63:v2"
    modImplementation "net.fabricmc:fabric-loader:0.11.7"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.42.1+1.17"
    modImplementation("com.terraformersmc:modmenu:2.0.4") {
        transitive = false
    }
    modImplementation("gg.essential:essential-1.17.1-fabric:1540+release-launch-1.17") {
        transitive = false
    }
    modImplementation("gg.essential:vigilance-1.17.1-fabric:179+next") {
        exclude(group: "net.fabricmc")
        exclude(group: "gg.essential")
    }
    modImplementation("gg.essential:universalcraft-1.17.1-fabric:162+next") {
        exclude(group: "net.fabricmc")
    }
    modImplementation("gg.essential:elementa-1.17.1-fabric:384+next") {
        exclude(group: "net.fabricmc")
        exclude(group: "gg.essential")
    }
    modImplementation include("gg.essential:loader-fabric:1.0.0") {
        transitive = false
    }
    modImplementation("net.fabricmc:fabric-language-kotlin:1.6.4+kotlin.1.5.30")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    it.options.release = 16
}

java {
    withSourcesJar()
}