plugins {
    // Languages
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'

    // Build
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'net.minecraftforge.gradle.forge' version '8708bf3e0'

    // Libraries
    id 'org.spongepowered.mixin' version '0.6-SNAPSHOT'
}

version = '1.0.0'
group = 'net.wyvest'
archivesBaseName = 'Wyvtilities'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }
}

minecraft {
    version = '1.8.9-11.15.1.2318-1.8.9'
    runDir = 'run'
    mappings = 'stable_22'

    makeObfSourceJar = false

    clientJvmArgs += '-Dfml.coreMods.load=net.wyvest.wyvtilities.mixin.WyvtilsLoadingPlugin'
    clientJvmArgs += '-Delementa.dev=true'
    clientRunArgs += '--tweakClass xyz.matthewtgm.requisite.launchwrapper.RequisiteLaunchwrapper'
    clientRunArgs += '--mixin mixins.wyvtils.json'
}

configurations {
    // Creates an extra configuration that extends from `implementation` to be used later as the configuration that shades libraries
    include
    implementation.extendsFrom include
}

dependencies {
    // Libraries
    provided 'xyz.matthewtgm:Requisite:1.0'
    include('xyz.matthewtgm:RequisiteLaunchwrapper:1.0') {
        transitive = false
    }
    include ('gg.essential:vigilance-1.8.9-forge:154') {
        exclude module: 'kotlin-reflect'
        exclude module: 'kotlin-stdlib-jdk8'
    }
    provided 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    provided('org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.21')
    provided('org.jetbrains.kotlin:kotlin-reflect:1.5.21')
    provided('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1-native-mt')
}

jar {
    manifest.attributes (
            'ModSide': 'CLIENT',
            'FMLCorePlugin': 'net.wyvest.wyvtilities.mixin.WyvtilsLoadingPlugin',
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'MixinConfigs': 'mixins.wyvtils.json',
            'TweakClass': 'xyz.matthewtgm.requisite.launchwrapper.RequisiteLaunchwrapper',
            'TweakOrder': '0'
    )

    enabled = false
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand (
                'version': project.version,
                'mcversion': project.minecraft.version
        )
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.reobfJar.dependsOn tasks.shadowJar

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.include]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    exclude 'LICENSE.md'
    exclude 'pack.mcmeta'
    exclude 'dummyThing'
    exclude '**/module-info.class'
    exclude '*.so'
    exclude '*.dylib'
    exclude '*.dll'
    exclude '*.jnilib'
    exclude 'ibxm/**'
    exclude 'com/jcraft/**'
    exclude 'org/lwjgl/**'
    exclude 'net/java/**'

    exclude 'META-INF/proguard/**'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/versions/**'
    exclude 'META-INF/com.android.tools/**'

    exclude 'fabric.mod.json'
}

repositories {
    mavenCentral()
    maven { url 'https://repo.sk1er.club/repository/maven-public/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

mixin {
    disableRefMapWarning = true
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.wyvtils.refmap.json"
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.wyvtils.refmap.json"
        output.resourcesDir = file("${buildDir}/classes/kotlin/main")
    }
}