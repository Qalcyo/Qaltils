plugins {
    id 'fabric-loom' version '0.9-SNAPSHOT'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.5.30'
}
sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    mixin.defaultRefmapName = "mixins.qaltils.refmap.json"
}

repositories {
    mavenCentral()
    maven { url "https://maven.shedaniel.me/" }
    maven {
        name 'Terraformers'
        url 'https://maven.terraformersmc.com/releases/'
    }
    maven { url 'https://repo.sk1er.club/repository/maven-releases/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation("com.terraformersmc:modmenu:2.0.4") {
        transitive = false
    }
    modImplementation('gg.essential:essential-1.17.1-fabric:1436+release-launch-1.17') {
        transitive = false
    }
    modImplementation include('gg.essential:loader-fabric:1.0.0') {
        transitive = false
    }
    modImplementation("net.fabricmc:fabric-language-kotlin:1.6.4+kotlin.1.5.30")
    modImplementation('gg.essential:vigilance-1.17-fabric:163+next') {
        exclude(group: "net.fabricmc")
        exclude module: 'kotlin-reflect'
        exclude module: 'kotlin-stdlib-jdk8'
    }
    modImplementation('com.electronwill.night-config:toml:3.6.4')
    modImplementation('com.electronwill.night-config:core:3.6.4')
    modImplementation("gg.essential:elementa-1.17-fabric:359+next") {
        exclude(group: "net.fabricmc")
        exclude module: 'kotlin-reflect'
        exclude module: 'kotlin-stdlib-jdk8'
    }
    //noinspection GradlePackageUpdate
    modImplementation("org.commonmark:commonmark:0.17.1")
    //noinspection GradlePackageUpdate
    modImplementation("org.commonmark:commonmark-ext-gfm-strikethrough:0.17.1")
    //noinspection GradlePackageUpdate
    modImplementation("org.commonmark:commonmark-ext-ins:0.17.1")
    //noinspection GradlePackageUpdate
    modImplementation("org.dom4j:dom4j:2.1.1")
    modImplementation("gg.essential:universalcraft-1.17-fabric:156+next") {
        exclude(group: "net.fabricmc")
        exclude module: 'kotlin-reflect'
        exclude module: 'kotlin-stdlib-jdk8'
    }

}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    it.options.release = 16
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }


    repositories {}
}